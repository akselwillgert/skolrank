apply plugin: 'com.android.application'


// Load application build configuration.
def Properties prodProperties = new Properties()
def buildProdFile = new File('build_prod.properties')
if (buildProdFile.canRead()) {
    prodProperties.load(new FileInputStream(buildProdFile))
} else {
    prodProperties.setProperty('GOOGLE_MAPS_API_KEY', "")
}

def Properties debugProperties = new Properties()
def buildDebugFile = new File('build_debug.properties')
if (buildDebugFile.canRead()) {
    debugProperties.load(new FileInputStream(buildDebugFile))
    if (!debugProperties.containsKey('GOOGLE_MAPS_API_KEY') || debugProperties.getProperty('GOOGLE_MAPS_API_KEY').equals("")) {
        prodProperties.setProperty('GOOGLE_MAPS_API_KEY', "")
        println 'missing GOOGLE_MAPS_API_KEY in build_debug.properties'
    }
} else {
    println "could not read build_debug.properties"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    repositories {
        jcenter()
        mavenLocal()
        maven { url "https://jitpack.io" }
        //    maven { url "https://repository-achartengine.forge.cloudbees.com/snapshot/" }
    }

    defaultConfig {
        applicationId "se.subsurface.schoolrankswe"
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 29
        versionName "0.0.29"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            resValue "string", "google_maps_api_key", prodProperties['GOOGLE_MAPS_API_KEY']
            minifyEnabled true
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            resValue "string", "google_maps_api_key", debugProperties['GOOGLE_MAPS_API_KEY']
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'
        exclude 'META-INF/services/com.fasterxml.jackson.core.ObjectCodec'
        return void
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.google.android.gms:play-services-maps:9.8.0'
    compile 'com.google.android.gms:play-services-analytics:9.8.0'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    compile 'de.psdev.licensesdialog:licensesdialog:1.7.0@aar'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.3'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.google.maps.android:android-maps-utils:0.4.4'
    compile 'de.greenrobot:eventbus:2.4.0'
}

// Fetch properties for signing
def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

apply plugin: 'com.google.gms.google-services'